// MODULE 2 - MEMORY MANAGER
alias functionNum R1;

// Get Free Page
if (functionNum == 1) then

    alias PID R2;

    // increment WAIT_MEM_COUNT (don't, if in busy loop)
    [SYSTEM_STATUS_TABLE + 3] = [SYSTEM_STATUS_TABLE + 3] + 1;

    // while memory is full (MEM_FREE_COUNT = 0)
    // wait for memory and schedule other processes meanwhile
    while ([SYSTEM_STATUS_TABLE + 2] == 0) do
        [PROCESS_TABLE + PID*16 + 4] = WAIT_MEM;
        multipush (R0, R1, R2);
        call CONTEXT_SWITCH;
        multipop (R0, R1, R2);
    endwhile;

    // decrement WAIT_MEM_COUNT & MEM_FREE_COUNT
    [SYSTEM_STATUS_TABLE + 2] = [SYSTEM_STATUS_TABLE + 2] - 1;
    [SYSTEM_STATUS_TABLE + 3] = [SYSTEM_STATUS_TABLE + 3] - 1;
    
    //loop through memory free list entries
    alias freePageNum R0;
    alias ctr R3;
    ctr = 76;
    while (ctr < 128) do
        if ([MEMORY_FREE_LIST + ctr] == 0) then
            [MEMORY_FREE_LIST + ctr] = 1;
            freePageNum = ctr;
            break;
        endif;
        ctr = ctr + 1;
    endwhile;
    return;
endif;

// Release Page
if (functionNum == 2) then
    
    alias pageNum R2;
    [MEMORY_FREE_LIST + pageNum] = [MEMORY_FREE_LIST + pageNum] - 1;
    
    if([MEMORY_FREE_LIST + pageNum] == 0) then
        // increment MEM_FREE_COUNT
        [SYSTEM_STATUS_TABLE + 2] = [SYSTEM_STATUS_TABLE + 2] + 1;
    endif;

    alias ctr R3;
    ctr = 0;
    while(ctr < 16) do
        if ([PROCESS_TABLE + ctr*16 + 4] == WAIT_MEM) then
            [PROCESS_TABLE + ctr*16 + 4] = READY;
        endif;
        ctr = ctr + 1;
    endwhile;
    return;
endif;


// Release block
if (functionNum == 4) then
    alias PID R2;
    alias blockNum R3;
    [DISK_FREE_LIST + blockNum] = [DISK_FREE_LIST + blockNum] - 1;
    return;
endif;


// Get Code Page
if (functionNum == 5) then
    // print("GetCodePg");
    alias PID R2;
    alias blockNumber R3;
    alias pageNumber R0;
    alias ctr R4;
    alias i R5;
    alias diskMapTableEntry R6;
    ctr = 0;
    while (ctr < 16) do
        diskMapTableEntry = DISK_MAP_TABLE + ctr*10;
        i = 4;
        while (i < 8 && [diskMapTableEntry + i] != -1) do
            if (([diskMapTableEntry + i] == blockNumber) && [PAGE_TABLE_BASE + ctr*20 + i*2] != -1) then
                pageNumber = [PAGE_TABLE_BASE + ctr*20 + i*2];
                [MEMORY_FREE_LIST + pageNumber] = [MEMORY_FREE_LIST + pageNumber] + 1;
                return;
            endif;
            i = i + 1;
        endwhile;
        ctr = ctr + 1;
    endwhile;

    // if blocknum was not found, it is not shared by other processes

    // invoke Get Free Page to allocate new memory page
    multipush(R1, R2, R3, R4, R5, R6);
    functionNum = 1;
    call MEMORY_MANAGER;
    multipop(R1, R2, R3, R4, R5, R6);

    //blocknumber in R3, pagenumber in R0, functionNum in R1

    // invoke Disk Load to load disk block to new memory page
    multipush(R1, R2, R3, R4, R5, R6);
    functionNum = 2;
    R4 = blockNumber;
    R3 = pageNumber;
    call DEVICE_MANAGER;
    multipop(R1, R2, R3, R4, R5, R6);
    // print("ExtGtCdPg");
    // breakpoint;
    return;
endif;

